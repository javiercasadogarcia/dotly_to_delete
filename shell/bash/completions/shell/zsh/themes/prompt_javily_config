#!/usr/bin/env zsh

JAVILY_THEME_MINIMAL=${JAVILY_THEME_MINIMAL:-false}
JAVILY_THEME_MODE=${JAVILY_THEME_MODE:-"dark"}
JAVILY_THEME_PROMPT_IN_NEW_LINE=${JAVILY_THEME_PROMPT_IN_NEW_LINE:-false}
JAVILY_THEME_PWD_MODE=${JAVILY_THEME_PWD_MODE:-"short"} # full, short, home_relative

JAVILY_THEME_STATUS_ICON_OK=${JAVILY_THEME_STATUS_ICON_OK:-"☺"}
JAVILY_THEME_STATUS_ICON_KO=${JAVILY_THEME_STATUS_ICON_KO:-"☹"}

 [[ $(echotc Co) -gt 100 ]] && support_color_tones=true || support_color_tones=false

git_no_changes_status="✓"
git_dirty_status="✗"

if [ "$JAVILY_THEME_MODE" = "dark" ]; then
  git_branch_color="green"
  git_dirty_status_color="yellow"
  git_no_changes_status_color="white"
  git_on_branch_color="white"
  pwd_color="yellow"
  diamond_color="white"
  status_icon_color_ok="green"
  status_icon_color_ko="red"
  [[ $support_color_tones = true ]] && ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=8' || ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=magenta'
else
  git_branch_color="black"
  git_dirty_status_color="magenta"
  git_no_changes_status_color="green"
  git_on_branch_color="green"
  pwd_color="black"
  diamond_color="black"
  status_icon_color_ok="black"
  status_icon_color_ko="red"
  [[ $support_color_tones = true ]] && ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=8' || ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=magenta'
fi

prompt_javily_pwd() {
  case "$JAVILY_THEME_PWD_MODE" in
    short) local -r prompt_dir=$("$DOTLY_PATH/bin/dot" core short_pwd) ;;
    full) local -r prompt_dir="$PWD" ;;
    home_relative) local -r prompt_dir=$(print -rD "$PWD") ;;
  esac

  print -n "%F{$pwd_color}${prompt_dir}"
}

prompt_javily_git() {
  [[ -n ${git_info} ]] && print -n "%F{$git_on_branch_color} on${(e)git_info[prompt]}"
}

prompt_javily_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_javily_config() {
  local prompt_javily_status="%(?:%F{diamond_color}<%F{$status_icon_color_ok}$JAVILY_THEME_STATUS_ICON_OK%F{diamond_color}>:%F{diamond_color}<%F{$status_icon_color_ko}$JAVILY_THEME_STATUS_ICON_KO%F{diamond_color}>)"

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_javily_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format "%F{$git_branch_color}%b"
  zstyle ':zim:git-info:commit' format "%c"
  zstyle ':zim:git-info:clean' format "%F{$git_no_changes_status_color}$git_no_changes_status"
  zstyle ':zim:git-info:dirty' format "%F{$git_dirty_status_color}$git_dirty_status"
  zstyle ':zim:git-info:keys' format "prompt" " %F{cyan}%b%c %C%D"

  if [ "$JAVILY_THEME_MINIMAL" = true ]; then
    PS1="${prompt_javily_status} \$(prompt_javily_pwd) "
  else
    PS1="${prompt_javily_status} \$(prompt_javily_pwd)\$(prompt_javily_git)%f "
  fi

  if [ "$JAVILY_THEME_PROMPT_IN_NEW_LINE" = true ]; then
    PS1="╭$PS1"$'\n%F{white}╰ '
  fi

  RPS1=''
}

prompt_javily_config "${@}"
